#!/usr/bin/perl

=head1 NAME

 set-engine-permissions Set i-MSCP engine permission

=head1 SYNOPSIS

 set-engine-permissions [OPTION]...

=cut

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2018 by Laurent Declercq <l.declercq@nuxwin.com>
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.

use strict;
use warnings;
use File::Basename 'basename';
use iMSCP::Boolean;
use iMSCP::Bootstrapper;
use iMSCP::Debug qw/ debug newDebug /;
use iMSCP::Getopt;
use iMSCP::LockFile;
use iMSCP::Rights 'setRights';
use iMSCP::Servers;
use iMSCP::Packages;
use POSIX qw/ locale_h /;

setlocale( LC_MESSAGES, 'C.UTF-8' );

$ENV{'LANG'} = 'C.UTF-8';

$> == 0 or die( "You do not have permission to call this script.\n" );

exit unless iMSCP::LockFile->new( path => "$::imscpConfig{'LOCK_DIR'}/imscp-set-engine-permissions.lock", non_blocking => TRUE)->acquire();

iMSCP::Getopt->parse( sprintf( 'Usage: perl %s [OPTION]...', basename( $0 )) . qq{

Set i-MSCP engine permissions.

OPTIONS:
 -d,   --debug           Enable debug mode.
 -h,-? --help            Show this help.
 -i,   --installer       Set installer context.
 -v,   --verbose Enable verbose mode.
 -x,   --fix-permissions Fix permissions recursively.
},
    'debug|d'           => \&iMSCP::Getopt::debug,
    'help|?|h'          => \&iMSCP::Getopt::showUsage,
    'installer|s'       => sub { iMSCP::Getopt->context( 'installer' ); },
    'verbose|v'         => \&iMSCP::Getopt::verbose,
    'fix-permissions|x' => \&iMSCP::Getopt::fixPermissions
);

newDebug( 'imscp-set-engine-permissions.log' );

iMSCP::Bootstrapper->getInstance()->boot( {
    config_readonly => TRUE,
    mode            => iMSCP::Getopt->context(),
    nodatabase      => TRUE
} );

my $rs = 0;
my @items = ();

for my $server ( iMSCP::Servers->getInstance()->getList() ) {
    push @items, [ $server, sub { $server->factory()->setEnginePermissions(); } ];
}

for my $package ( iMSCP::Packages->getInstance()->getList() ) {
    push @items, [ $package, sub { $package->getInstance()->setEnginePermissions(); } ];
}

my $totalItems = scalar @items+1;
my $count = 1;

debug( 'Setting base (engine) permissions' );
printf( "Setting base (engine) permissions\t%s\t%s\n", $totalItems, $count ) if iMSCP::Getopt->context() eq 'installer';

# e.g: /usr/local/etc/imscp
setRights( $::imscpConfig{'CONF_DIR'}, {
    user      => $::imscpConfig{'ROOT_USER'},
    group     => $::imscpConfig{'IMSCP_GROUP'},
    dirmode   => '0750',
    filemode  => '0640',
    recursive => TRUE
} );
# e.g: /var/www/imscp
setRights( $::imscpConfig{'ROOT_DIR'}, {
    user  => $::imscpConfig{'ROOT_USER'},
    group => $::imscpConfig{'ROOT_GROUP'},
    mode  => '0755'
} );
# e.g: /var/www/imscp/daemon
setRights( "$::imscpConfig{'ROOT_DIR'}/daemon", {
    user      => $::imscpConfig{'ROOT_USER'},
    group     => $::imscpConfig{'IMSCP_GROUP'},
    mode      => '0750',
    recursive => TRUE
} );
# e.g: /var/www/imscp/engine
setRights( "$::imscpConfig{'ROOT_DIR'}/engine", {
    user      => $::imscpConfig{'ROOT_USER'},
    group     => $::imscpConfig{'IMSCP_GROUP'},
    mode      => '0750',
    recursive => TRUE
} );
# e.g: /var/www/virtual
setRights( $::imscpConfig{'SRV_DIR'}, {
    user  => $::imscpConfig{'ROOT_USER'},
    group => $::imscpConfig{'ROOT_GROUP'},
    mode  => '0755'
} );
# e.g: /var/log/imscp
setRights( $::imscpConfig{'LOG_DIR'}, {
    user  => $::imscpConfig{'ROOT_USER'},
    group => $::imscpConfig{'IMSCP_GROUP'},
    mode  => '0750'
} );

$count++;

for my $item( @items ) {
    debug( sprintf( 'Setting %s engine permissions', $item->[0] ));
    printf( "Setting %s engine permissions\t%s\t%s\n", $item->[0], $totalItems, $count++ ) if iMSCP::Getopt->context() eq 'installer';
    $item->[1]->();
}

exit $rs;

=head1 AUTHOR

 Laurent Declercq <l.declercq@nuxwin.com>

=cut

1;
__END__
