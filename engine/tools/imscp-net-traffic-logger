#!/usr/bin/perl

=head1 NAME

 imscp-net-traffic-logger - start/stop i-MSCP traffic logger.

=head1 SYNOPSIS

 imscp-net-traffic-logger [OPTION]... start|stop

=cut

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2019 by Laurent Declercq <l.declercq@nuxwin.com>
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.

use strict;
use warnings;
use FindBin;
use lib "$FindBin::Bin/../PerlLib", "$FindBin::Bin/../PerlVendor";
use File::Basename;
use iMSCP::Boolean;
use iMSCP::Debug qw/ debug error newDebug setDebug setVerbose /;
use iMSCP::Bootstrapper;
use iMSCP::Execute 'execute';
use iMSCP::Getopt;
use iMSCP::ProgramFinder;

@{ENV}{qw/ LANG PATH /} = (
    'C.UTF-8',
    '/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin'
);

# Set execution context
$::execmode = 'backend';

my @EXECS;

sub start
{
    my $panelHttpPort = $::imscpConfig{'BASE_SERVER_VHOST_HTTP_PORT'} || undef;
    my $panelHttpsPort = $::imscpConfig{'BASE_SERVER_VHOST_HTTPS_PORT'} || undef;

    for my $exec ( @EXECS ) {
        defined $exec or next;

        # Creating IMSCP_INPUT and IMSCP_OUTPUT chains for incoming/outgoing
        # traffic logging
        for my $chain ( qw/ IMSCP_INPUT IMSCP_OUTPUT / ) {
            my $rs = execute(
                "$exec -w -t filter -N $chain", \my $stdout, \my $stderr
            );
            debug( $stdout ) if $stdout;
            error( $stderr || 'Unknown error' ) if $rs;
            return $rs if $rs;
        }

        # Adding rules to pass incoming/outgoing traffic through our chains,
        # excluding traffic from/to 'lo' interface
        for my $rule ( '-I INPUT ! -i lo -j IMSCP_INPUT', '-I OUTPUT ! -o lo -j IMSCP_OUTPUT' ) {
            my $rs = execute(
                "$exec -w -t filter $rule", \my $stdout, \my $stderr
            );
            debug( $stdout ) if $stdout;
            error( $stderr || 'Unknown error' ) if $rs;
            return $rs if $rs;
        }

        for my $port (
            $panelHttpPort,
            $panelHttpsPort,
            80, 443, 110, 143, 25, 465, 587, 995, 993, 20, 21
        ) {
            defined or next;

            # Adding rule for logging incoming traffic to the given port
            my $rs = execute(
                "$exec -w -t filter -I IMSCP_INPUT -p tcp --dport $port",
                \my $stdout,
                \my $stderr
            );
            debug( $stdout ) if $stdout;
            error( $stderr || 'Unknown error' ) if $rs;
            return $rs if $rs;

            # Adding rule for logging outgoing traffic from the given port
            $rs = execute(
                "$exec -w -t filter -I IMSCP_OUTPUT -p tcp --sport $port",
                \$stdout,
                \$stderr
            );
            debug( $stdout ) if $stdout;
            error( $stderr || 'Unknown error' ) if $rs;
            return $rs if $rs;
        }

        for my $port ( qw/ 25 465 587 / ) {
            # Adding a rule for logging incoming traffic from the given port
            my $rs = execute(
                "$exec -w -t filter -I IMSCP_INPUT -p tcp --sport $port",
                \my $stdout,
                \my $stderr
            );
            debug( $stdout ) if $stdout;
            error( $stderr || 'Unknown error' ) if $rs;
            return $rs if $rs;

            # Adding a rule for logging outgoing traffic to the given port
            $rs = execute(
                "$exec -w -t filter -I IMSCP_OUTPUT -p tcp --dport $port",
                \$stdout,
                \$stderr
            );
            debug( $stdout ) if $stdout;
            error( $stderr || 'Unknown error' ) if $rs;
            return $rs if $rs;
        }

        for my $chain ( qw/ IMSCP_INPUT IMSCP_OUTPUT / ) {
            my $rs = execute(
                "$exec -w -t filter -A $chain -j RETURN",
                \my $stdout,
                \my $stderr
            );
            debug( $stdout ) if $stdout;
            error( $stderr || 'Unknown error' ) if $rs;
            return $rs if $rs;
        }
    }

    0;
}

sub stop
{
    for my $exec ( @EXECS ) {
        defined $exec or next;

        my $rs = execute(
            "$exec -S | grep 'IMSCP_'", \my $stdout, \my $stderr
        );
        debug( $stdout ) if $stdout;
        error( $stderr || 'Unknown error' ) if $rs > 1;
        return $rs if $rs > 1;

        next unless $stdout;

        my %data;
        while ( $stdout =~ /^-(A|N)\s+(((?:IMSCP_)?(?:INPUT|OUTPUT)).*)/gmi ) {
            if ( $1 eq 'A' && index( $3, 'IMSCP' ) != 0 ) {
                push @{ $data{'delete'} }, $2;
                next;
            }

            $data{'flush'}->{$3} = 1 unless $data{'flush'}->{$3};
        };

        # Delete i-MSCP rules on INPUT and OUTPUT chains if any
        if ( $data{'delete'} ) {
            for my $rule ( @{ $data{'delete'} } ) {
                $rs = execute(
                    "$exec -w -t filter -D $rule", \$stdout, \$stderr
                );
                debug( $stdout ) if $stdout;
                error( $stderr || 'Unknown error' ) if $rs;
                return $rs if $rs;
            }
        }

        # Flush and delete i-MSCP chains if any
        if ( $data{'flush'} ) {
            for my $chain ( keys %{ $data{'flush'} } ) {
                $rs = execute(
                    "$exec -w -t filter -F $chain", \$stdout, \$stderr
                );
                debug( $stdout ) if $stdout;
                error( $stderr || 'Unknown error' ) if $rs;
                return $rs if $rs;

                $rs ||= execute(
                    "$exec -w -t filter -X $chain", \$stdout, \$stderr
                );
                debug( $stdout ) if $stdout;
                error( $stderr || 'Unknown error' ) if $rs;
                return $rs if $rs;
            }
        }
    }

    0;
}

iMSCP::Getopt->parseNoDefault( sprintf( 'Usage: perl %s [OPTION]... start|stop', basename( $0 )) . qq{

Script that start/stop i-MSCP traffic logger.

OPTIONS:
 -d,    --debug         Enable debug mode.
 -v,    --verbose       Enable verbose mode.},
    'debug|d'   => \&iMSCP::Getopt::debug,
    'verbose|v' => \&iMSCP::Getopt::verbose
);

my $action = shift;
$action && $action =~ /^(?:start|stop)$/ or iMSCP::Getopt->showUsage();

newDebug( 'imscp-net-traffic-logger.log' );
setDebug( iMSCP::Getopt->debug );
setVerbose( iMSCP::Getopt->verbose );

exit unless iMSCP::Bootstrapper->getInstance()->lock(
    '/var/lock/imscp-net-traffic-logger.lock', 'nowait'
);

iMSCP::Bootstrapper->getInstance()->boot( {
    config_readonly => TRUE,
    nodatabase      => TRUE,
    nokeys          => TRUE,
    mode            => $::execmode,
    nolock          => TRUE
} );

@EXECS = (
    iMSCP::ProgramFinder::find( 'iptables' ),
    ( $::imscpConfig{'IPV6_SUPPORT'}
        ? iMSCP::ProgramFinder::find( 'ip6tables' ) : undef
    )
);

my %dispatch = (
    start => sub { stop() || start() },
    stop  => \&stop
);

exit $dispatch{ $action }->();

=head1 AUTHOR

 Laurent Declercq <l.declercq@nuxwin.com>

=cut

1;
__END__
