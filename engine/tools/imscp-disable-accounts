#!/usr/bin/perl

=head1 NAME

 imscp-disable-accounts - Deactivates expired client accounts

=head1 SYNOPSIS

 imscp-disable-accounts [options]...

=cut

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2018 Laurent Declercq <l.declercq@nuxwin.com>
#
# This library is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public
# License as published by the Free Software Foundation; either
# version 2.1 of the License, or (at your option) any later version.
#
# This library is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this library; if not, write to the Free Software
# Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA

use strict;
use warnings;
use FindBin;
use lib "$FindBin::Bin/../PerlLib";
use iMSCP::Database;
use iMSCP::Debug qw/ debug error newDebug /;
use iMSCP::Bootstrapper;
use iMSCP::DbTasksProcessor;
use iMSCP::Getopt;
use File::Basename;
use POSIX qw/ locale_h /;

setlocale( LC_MESSAGES, "C.UTF-8" );

$ENV{'LANG'} = 'C.UTF-8';

sub run
{
    my $dbh = iMSCP::Database->getInstance();

    eval {
        my $row = $dbh->selectrow_hashref( "SELECT value FROM config WHERE name = 'HARD_MAIL_SUSPENSION'" );
        $row = { value => 0 } unless $row;

        my $rows = $dbh->selectall_hashref(
            "
                SELECT domain_id, domain_admin_id
                FROM domain
                WHERE domain_expires <> '0'
                AND domain_expires < UNIX_TIMESTAMP()
                AND domain_status <> 'disabled'
            ",
            'domain_id'
        );

        return unless %{ $rows };

        for my $domainId ( keys %{ $rows } ) {
            eval {
                $dbh->begin_work();

                if ( $row->{'value'} ) { # SMTP/IMAP/POP disabled
                    $dbh->do( 'UPDATE mail_users SET status = ?, po_active = ? WHERE domain_id = ?', undef, 'todisable', 'no', $domainId );
                } else { # IMAP/POP disabled
                    $dbh->do( 'UPDATE mail_users SET po_active = ? WHERE domain_id = ?', undef, 'no', $domainId );
                }

                $dbh->do( 'UPDATE domain SET domain_status = ? WHERE domain_id = ?', undef, 'todisable', $domainId );
                $dbh->do( 'UPDATE subdomain SET subdomain_status = ? WHERE domain_id = ?', undef, 'todisable', $domainId );
                $dbh->do( 'UPDATE domain_aliasses SET alias_status = ? WHERE domain_id = ?', undef, 'todisable', $domainId );
                $dbh->do(
                    '
                        UPDATE subdomain_alias SET subdomain_alias_status = ?
                        WHERE alias_id IN (SELECT alias_id FROM domain_aliasses WHERE domain_id = ?)
                    ',
                    undef, 'todisable', $domainId
                );
                $dbh->commit();
            };
            if ( $@ ) {
                $dbh->rollback();
                error( $@ );
            }
        }

        iMSCP::DbTasksProcessor->getInstance()->processDbTasks();
    };
    !$@ or error( $@ );
}

newDebug( 'imscp-disable-accounts.log' );

iMSCP::Getopt->parseNoDefault( sprintf( 'Usage: perl %s [OPTION]...', basename( $0 )) . qq{

Script that disable expired customer accounts

OPTIONS:
 -d,    --debug         Enable debug mode.
 -v,    --verbose       Enable verbose mode.},
    'debug|d'   => \&iMSCP::Getopt::debug,
    'verbose|v' => \&iMSCP::Getopt::verbose
);

exit unless iMSCP::Bootstrapper->getInstance()->getInstance()->boot( {
    config_readonly => 1
} )->lock( "$::imscpConfig{'LOCK_DIR'}/imscp-disable-accounts.lock', 'nowait" );

run();

=head1 AUTHOR

 Laurent Declercq <l.declercq@nuxwin.com>

=cut

1;
__END__
