#!/usr/bin/perl

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2018 by internet Multi Server Control Panel
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.

=head1 NAME

 imscp-uninstall Uninstall i-MSCP

=head1 SYNOPSIS

 imscp-uninstall [options]...

=cut

# TODO: This script should be idempotent.

use strict;
use warnings;
use FindBin;
use lib "$FindBin::Bin/../PerlLib", "$FindBin::Bin/../PerlVendor";
use File::Basename;
use iMSCP::Boolean;
use iMSCP::Bootstrapper;
use iMSCP::Database;
use iMSCP::DbTasksProcessor;
use iMSCP::Debug qw/ error newDebug /;
use iMSCP::Dialog;
use iMSCP::Dir;
use iMSCP::DistPackageManager;
use iMSCP::File;
use iMSCP::Getopt;
use iMSCP::Packages;
use iMSCP::Servers;
use iMSCP::Service;
use iMSCP::Stepper;
use iMSCP::SystemUser;

use Servers::sqld;

$ENV{'LANG'} = 'C.UTF-8';
$ENV{'PATH'} = '/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin';

# Set execution context
iMSCP::Getopt->context( 'uninstaller' );

newDebug( 'imscp-uninstall.log' );

sub startUp
{
    iMSCP::Getopt->debug( TRUE ); # Force debug mode (logs)
    iMSCP::Bootstrapper->getInstance()->boot();
    0;
}

sub process
{
    _userDialog();

    {
        my $rdbh = iMSCP::Database->factory()->getRawDb();
        local $rdbh->{'RaiseError'} = TRUE;

        my $row = $rdbh->selectrow_hashref( "SELECT 1 FROM plugin WHERE plugin_status <> 'uninstalled'" );
        if ( $row ) {
            iMSCP::Dialog->getInstance()->infobox( <<'EOF' );
\Z1Uninstallation aborted.\Zn

You must first uninstall all i-MSCP plugins through the control panel interface.
EOF
            exit 0;
        }
    }

    my @steps = (
        [ \&_removeCustomers, 'Removing i-MSCP customers' ],
        [ \&_uninstallPackagesAndServers, 'Uninstalling i-MSCP packages and servers' ],
        [ \&_uninstallServices, 'Uninstalling i-MSCP services' ],
        [ \&_removeCrontabFile, 'Removing i-MSCP crontab file' ],
        [ \&_removeLogrotateFile, 'Removing i-MSCP logrotate file' ],
        [ \&_removeSysctlFile, 'Removing i-MSCP sysctl file' ],
        [ \&_removeSqlData, 'Removing i-MSCP SQL data' ],
        [ \&_removeiMSCPSystemUser, 'Removing i-MSCP system user' ],
        [ \&_removeFiles, 'Removing i-MSCP files' ],
    );

    my ( $step, $rs ) = ( 1, 0 );
    for ( @steps ) {
        $rs = step( @{ $_ }, scalar @steps, $step );
        last if $rs;
        $step++;
    }

    $rs ||= iMSCP::Dialog->getInstance()->endGauge();
}

sub shutDown
{

    iMSCP::Dialog->getInstance()->infobox( <<'EOF' );

\Z1Congratulations\Zn

iMSCP has been successfully uninstalled.

You can find uninstallation logs at /var/log/imscp/imscp-uninstall.log
EOF

    0;
}

sub _userDialog
{
    my $dialog = iMSCP::Dialog->getInstance();
    $dialog->set( 'title', 'i-MSCP Uninstaller Dialog' );

    exit 0 if $dialog->yesno( <<"EOF", 1 );

Welcome to the \\Z4i-MSCP $::imscpConfig{'Version'}\\Zn uninstaller dialog.

This program will uninstall i-MSCP from your server.

\\Z1WARNING:\\Zn All i-MSCP data will be removed.

Do you want to continue?
EOF
}

sub _removeCustomers
{
    {
        my $tables = {
            admin           => [ 'admin_status', "WHERE admin_type = 'user'" ],
            domain          => 'domain_status',
            domain_aliasses => 'alias_status',
            subdomain       => 'subdomain_status',
            subdomain_alias => 'subdomain_alias_status',
            mail_users      => 'status',
            htaccess        => 'status',
            htaccess_groups => 'status',
            htaccess_users  => 'status',
            ssl_certs       => 'status'
        };
        my $aditionalCondition;

        my $db = iMSCP::Database->factory();
        my $oldDbName = $db->useDatabase( $::imscpConfig{'DATABASE_NAME'} );

        my $rdbh = $db->getRawDb();
        local $rdbh->{'RaiseError'} = TRUE;

        while ( my ( $table, $field ) = each %{ $tables } ) {
            if ( ref $field eq 'ARRAY' ) {
                $aditionalCondition = $field->[1];
                $field = $field->[0];
            } else {
                $aditionalCondition = ''
            }

            ( $table, $field ) = ( $rdbh->quote_identifier( $table ), $rdbh->quote_identifier( $field ) );
            $rdbh->do( "UPDATE $table SET $field = 'todelete' $aditionalCondition" );
        }

        $db->useDatabase( $oldDbName ) if $oldDbName;
    }

    startDetail();
    iMSCP::DbTasksProcessor->getInstance( mode => iMSCP::Getopt->context() )->processDbTasks();
    endDetail();

    0;
}

sub _uninstallPackagesAndServers
{
    my @servers = iMSCP::Servers->getInstance()->getList();
    my @packages = iMSCP::Packages->getInstance()->getList();
    my @actions = ( 'preuninstall', 'uninstall', 'postuninstall' );
    my $nbSteps = (@servers+@packages) * @actions;
    my ( $rs, $step ) = ( 0, 1 );

    ACTION:
    for my $action ( @actions ) {
        startDetails();
        for my $package ( packages ) {
            $rs = step( sub { $package->getInstance()->$action() }, sprintf( "Executing %s %s tasks...", $package, $action ), $nbSteps, $step );
            last ACTION if $rs;
            $step++;
        }
        for my $server ( @servers ) {
            $rs = step( sub { $server->factory()->$action() }, sprintf( "Executing %s %s tasks...", $server, $action ), $nbSteps, $step );
            last ACTION if $rs;
            $step++;
        }
        endDetail();
    }

    iMSCP::DistPackageManager->getInstance()->processDelayedTasks();
    
    endDetail() if $rs;
    $rs;
}

sub _uninstallServices
{
    my $serviceMngr = iMSCP::Service->getInstance();
    for my $service ( 'imscp_daemon', 'imscp_traffic', 'imscp_mountall' ) {
        $serviceMngr->remove( $service );
    }

    for my $service ( 'bind9', 'bind9-resolvconf' ) {
        $serviceMngr->disable( $service ) if $serviceMngr->isSystemd();

        if ( -f "/etc/systemd/system/$service.service" ) {
            my $rs = iMSCP::File->new( filename => "/etc/systemd/system/$service.service" )->delFile();
            return $rs if $rs;
        }

        if ( "/lib/systemd/system/$service.service" ) {
            $serviceMngr->enable( $service ) if $serviceMngr->isSystemd();
        }
    }

    0;
}

sub _removeCrontabFile
{
    return 0 unless -f '/etc/cron.d/imscp';
    iMSCP::File->new( filename => '/etc/cron.d/imscp' )->delFile();
}

sub _removeLogrotateFile
{
    return 0 unless -f "$::imscpConfig{'LOGROTATE_CONF_DIR'}/imscp";
    iMSCP::File->new( filename => "$::imscpConfig{'LOGROTATE_CONF_DIR'}/imscp" )->delFile();
}

sub _removeSysctlFile
{
    return 0 unless -f '/etc/sysctl.d/imscp.conf';
    iMSCP::File->new( filename => '/etc/sysctl.d/imscp.conf' )->delFile();
}

sub _removeSqlData
{

    my $sqlServer = Servers::sqld->factory();
    my $rdbh = iMSCP::Database->factory()->getRawDb();

    # Remove customer's sql users
    my $rows = $rdbh->selectall_hashref( 'SELECT DISTINCT sqlu_name, sqlu_host FROM sql_user', 'sqlu_name' );
    $sqlServer->dropUser( $rows->{$_}->{'sqlu_name'}, $rows->{$_}->{'sqlu_host'} ) for keys %{ $rows };
    # Remove customer's sql databases
    $rows = $rdbh->selectall_hashref( 'SELECT sqld_name FROM sql_database', 'sqld_name' );
    $rdbh->do( 'DROP DATABASE IF EXISTS ' . $rdbh->quote_identifier( $rows->{$_}->{'sqld_name'} )) for keys %{ $rows };
    # Remove i-MSCP sql database
    $rdbh->do( 'DROP DATABASE IF EXISTS ' . $rdbh->quote_identifier( $::imscpConfig{'DATABASE_NAME'} ));
    # Drop i-MSCP master SQL user
    $sqlServer->dropUser( $::imscpConfig{'DATABASE_USER'}, $::imscpConfig{'DATABASE_USER_HOST'} );
    0;
}

sub _removeiMSCPSystemUser
{
    iMSCP::SystemUser->new( force => 1 )->delSystemUser( $::imscpConfig{'IMSCP_USER'} );
}

sub _removeFiles
{
    chdir( '/' );

    for my $file ( '/usr/local/sbin/imscp-dpkg-post-invoke.pl', '/etc/apt/apt.conf.d/90imscp' ) {
        next unless -f $file;
        my $rs = iMSCP::File->new( filename => $file )->delFile();
        return $rs if $rs;
    }

    for my $dir ( $::imscpConfig{'CONF_DIR'}, $::imscpConfig{'USER_WEB_DIR'}, $::imscpConfig{'LOG_DIR'}, $::imscpConfig{'ROOT_DIR'} ) {
        iMSCP::Dir->new( dirname => $dir )->remove();
    }

    0;
}

my $rs = 0;
$rs ||= startUp();
$rs ||= process();
$rs ||= shutDown();
exit $rs;

=head1 AUTHOR

 i-MSCP Team <team@i-mscp.net>

=cut

1;
__END__
