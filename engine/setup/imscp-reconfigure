#!/usr/bin/perl

=head1 NAME

 imscp-reconfigure Reconfigure i-MSCP

=head1 SYNOPSIS

 imscp-reconfigure [OPTION]...

=cut

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2019 by Laurent Declercq <l.declercq@nuxwin.com>
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.

use strict;
use warnings;
use File::Basename 'basename';
use FindBin;
use lib "$FindBin::Bin/../PerlLib", "$FindBin::Bin/../PerlVendor";
use iMSCP::Boolean;
use iMSCP::Debug qw/ newDebug setDebug setVerbose output /;
use iMSCP::Dialog;
use iMSCP::EventManager;
use iMSCP::Getopt;
use Net::LibIDN 'idn_to_unicode';
use POSIX 'locale_h';

@{ENV}{qw/ LANG PATH IMSCP_SETUP /} = (
    'C.UTF-8',
    '/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin',
    TRUE
);
delete $ENV{'LANGUAGE'};
setlocale( LC_MESSAGES, 'C.UTF-8' );

{
    no warnings 'once';
    $::execmode = 'setup';
    %::questions = ()
}

iMSCP::Getopt->parse( sprintf( 'Usage: perl %s [OPTION]...', basename( $0 )));

newDebug( 'imscp-reconfigure.log' );
setDebug( iMSCP::Getopt->debug );
setVerbose( iMSCP::Getopt->noprompt && iMSCP::Getopt->verbose );

if ( iMSCP::Getopt->preseed ) {
    require iMSCP::Getopt->preseed;
    iMSCP::Getopt->reconfigure( 'none' );
    iMSCP::Getopt->noprompt( TRUE );
}

{
    no warnings 'once';
    $::reconfigure = iMSCP::Getopt->reconfigure;
}

sub run
{
    require "$FindBin::Bin/imscp-setup-functions.pl";

    my $rs = setupBoot();
    $rs ||= iMSCP::EventManager->getInstance()->trigger( 'beforeSetup' );
    $rs ||= setupRegisterListeners();
    return $rs if $rs;

    unless ( iMSCP::Getopt->noprompt ) {
        my $dialog = iMSCP::Dialog->getInstance();
        
        # Disable backup feature for first dialog
        $dialog->backup( FALSE );

        # Override default button labels
        local @{ $dialog->{'_opts'} }{
            $dialog->{'program'} eq 'dialog'
                ? qw/ title yes-label no-label /
                : qw/ title yes-button no-button /
        } = qw/ Reconfiguration Continue Abort /;

        if($dialog->boolean( <<"EOF", TRUE )) {
        
Welcome to the \\Z1\\Zbi-MSCP $::imscpConfig{'Version'}\\Zn reconfiguration dialog.

This program will reconfigures your i-MSCP instance.

During the process, some or all services might require to be shutdown or restarted.

You can now either continue or abort.
EOF
            system('clear');
            exit 0;
        }
    }

    $rs ||= setupDialog();
    $rs ||= setupTasks();
    $rs ||= iMSCP::EventManager->getInstance()->trigger( 'afterSetup' );
    return $rs if $rs || iMSCP::Getopt->noprompt;

    if ( iMSCP::Getopt->noprompt ) {
        if ( iMSCP::Getopt->verbose ) {
            print output( 'i-MSCP has been successfully reconfigured.', 'ok' );
        }
    } else {
        my $dialog = iMSCP::Dialog->getInstance();

        # Disable backup feature for last dialog
        $dialog->backup( FALSE );

        # Override default button label
        local $dialog->{'_opts'}->{
            $dialog->{'program'} eq 'dialog' ? 'ok-label' : 'ok-button'
        } = 'Ok';

        iMSCP::Dialog->getInstance()->note( <<"EOF" );
\\Z1\\ZbCongratulations\\Zn

i-MSCP has been successfully reconfigured.

You can login at $::imscpConfig{'BASE_SERVER_VHOST_PREFIX'}@{ [
    idn_to_unicode( $::imscpConfig{'BASE_SERVER_VHOST'}, 'utf-8' )
] }:@{ [ $::imscpConfig{'BASE_SERVER_VHOST_PREFIX'} eq 'http://'
    ? $::imscpConfig{'BASE_SERVER_VHOST_HTTP_PORT'}
    : $::imscpConfig{'BASE_SERVER_VHOST_HTTPS_PORT'}
] } with the master administrator account credentials.

Thank you for choosing i-MSCP.
EOF
        system( 'clear' );
    }

    0;
}

exit run();

=head1 AUTHOR

 Laurent Declercq <l.declercq@nuxwin.com>

=cut

1;
__END__
