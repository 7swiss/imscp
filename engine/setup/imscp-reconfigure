#!/usr/bin/perl

=head1 NAME

 imscp-reconfigure Reconfigure i-MSCP

=head1 SYNOPSIS

 imscp-reconfigure [OPTION]...

=cut

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2018 Laurent Declercq <l.declercq@nuxwin.com>
#
# This library is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public
# License as published by the Free Software Foundation; either
# version 2.1 of the License, or (at your option) any later version.
#
# This library is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this library; if not, write to the Free Software
# Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA

use strict;
use warnings;
use File::Basename;
use FindBin;
use lib "$FindBin::Bin/../PerlLib";
use iMSCP::Debug qw/ newDebug output /;
use iMSCP::Dialog;
use iMSCP::EventManager;
use iMSCP::Getopt;
use Net::LibIDN qw/ idn_to_unicode /;
use POSIX qw/ locale_h /;

setlocale( LC_MESSAGES, "C.UTF-8" );

$ENV{'LANG'} = 'C.UTF-8';

newDebug( 'imscp-setup.log' );

# Init variable that holds questions
%::questions = () unless %::questions;

# Set execution context
iMSCP::Getopt->context( 'installer' );

# Parse command line options
iMSCP::Getopt->parse( sprintf( 'Usage: perl %s [OPTION]...', basename( $0 )) . qq{
 -s,    --skip-distro-packages  Do not install/update distro packages.},
    'skip-distro-packages|s' => \&iMSCP::Getopt::skippackages
);

if ( iMSCP::Getopt->preseed ) {
    # The preseed option supersede the reconfigure option
    iMSCP::Getopt->reconfigure( 'none' );
    # The preseed option involves the noprompt option
    iMSCP::Getopt->noprompt( 1 );
}

# Inhibit verbose mode if we are not in non-interactive mode
iMSCP::Getopt->verbose( 0 ) unless iMSCP::Getopt->noprompt;

system( 'clear 2>/dev/null' );

print STDOUT output( 'Reconfiguration in progress... Please wait.', 'info' ) if iMSCP::Getopt->noprompt;

unless ( iMSCP::Getopt->noprompt ) {
    my $dialog = iMSCP::Dialog->getInstance();
    $dialog->set( 'title', 'i-MSCP Reconfiguration Dialog' );

    local $dialog->{'opts'}->{'yes-label'} = 'Continue';
    local $dialog->{'opts'}->{'no-label'} = 'Abort';

    exit 0 if $dialog->yesno( <<"EOF", 'abort_by_default' );

Welcome to the \\Z1i-MSCP $::imscpConfig{'Version'}\\Zn reconfiguration dialog.

This program will reconfigures your i-MSCP instance.

During the process, some or all services might require to be shutdown or restarted.

You can now either continue or abort.
EOF

    $dialog->resetLabels();
}

require "$FindBin::Bin/imscp-setup-methods.pl";

setupBoot();
setupRegisterListeners();
setupDialog();
setupTasks();

iMSCP::EventManager->getInstance()->trigger( 'afterSetup' );

return if iMSCP::Getopt->noprompt;

my $port = $::imscpConfig{'BASE_SERVER_VHOST_PREFIX'} eq 'http://'
    ? $::imscpConfig{'BASE_SERVER_VHOST_HTTP_PORT'} : $::imscpConfig{'BASE_SERVER_VHOST_HTTPS_PORT'};
my $vhost = idn_to_unicode( $::imscpConfig{'BASE_SERVER_VHOST'}, 'utf-8' ) // '';

iMSCP::Dialog->getInstance()->infobox( <<"EOF" );

\\Z1Congratulations\\Zn

i-MSCP has been successfully reconfigured.

Please connect to $::imscpConfig{'BASE_SERVER_VHOST_PREFIX'}$vhost:$port and login with your administrator account.

Thank you for choosing i-MSCP.
EOF

=head1 AUTHOR

 Laurent Declercq <l.declercq@nuxwin.com>

=cut

1;
__END__
