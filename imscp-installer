#!/usr/bin/perl

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2018 Laurent Declercq <l.declercq@nuxwin.com>
#
# This library is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public
# License as published by the Free Software Foundation; either
# version 2.1 of the License, or (at your option) any later version.
#
# This library is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this library; if not, write to the Free Software
# Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA

# Wrap real i-MSCP installer for satisfying pre-requisites
#
# Only base (Perl core) modules *MUST* be used here, that is, those which are
# made available after a base distribution installation.

use strict;
use warnings;
use POSIX qw/ locale_h /;
use Cwd qw/ abs_path /;

$ENV{'LANG'} = 'C.UTF-8';
setlocale( LC_MESSAGES, 'C.UTF-8' );

my $noansi = grep ( $_ =~ /^-(?:\b[^-]*z[^-]*\b|-no-ansi$)/, @ARGV );

sub prntInfo
{
    if ( $noansi ) {
        print STDOUT "[INFO]  @_\n";
        return;
    }

    print STDOUT "[\x1b[0;34mINFO\x1b[0m]  @_\n";
}

sub prntError
{
    if ( $noansi ) {
        print STDERR "[ERROR] @_\n";
        exit 1;
    }

    print STDERR "[\x1b[0;31mERROR\x1b[0m] @_\n";
    exit 1;
}

$> == 0 or prntError( 'The i-MSCP installer must be run with the privileges of super user.' );

chomp( our $ROOTDIR = `dirname @{ [ abs_path __FILE__ ] } 2>/dev/null` );
chdir( $ROOTDIR ) or prntError( sprintf( "couldn't chdir: %s", $! || 'Unknown error' ));
unshift @INC, ( $ROOTDIR, "$ROOTDIR/engine/PerlLib" );
system( 'clear 2>/dev/null' );

DIST_BOOTSTRAP:

my $forceDistBootstrap;
if ( $forceDistBootstrap || !-f '/etc/imscp/imscp.conf' ) {
    my ( $bootstrapDir, $bootstrapFile ) = ( "$ROOTDIR/installer/bootstrap" );

    # Basic heuristic which should work in most cases
    if ( -f '/etc/debian_version' || -f '/etc/devuan_version' ) {
        $bootstrapFile = 'debian.pl';
    } elsif ( -f '/etc/mageia-release' ) {
        $bootstrapFile = 'mageia.pl';
    } elsif ( -f '/etc/redhat-release' ) {
        $bootstrapFile = 'redhat.pl';
    } elsif ( -f '/etc/os-release' && `grep -q openSUSE /etc/os-release` ) {
        $bootstrapFile = 'opensuse.pl';
    } elsif ( -f '/etc/arch-release' || -f '/etc/manjaro-release' ) {
        $bootstrapFile = 'archlinux.pl';
    } elsif ( -f '/etc/gentoo-release' ) {
        $bootstrapFile = 'gentoo.pl';
    } else {
        prntError( 'Your distribution is not known yet. Please contact the i-MSCP team.' );
    }

    -f "$bootstrapDir/$bootstrapFile" or prntError(
        "No installer bootstrap file found for your distribution. You need first implement the $bootstrapDir/$bootstrapFile bootstrap file."
    );

    do "$bootstrapDir/$bootstrapFile" or prntError( sprintf( "Couldn't bootstrap the i-MSCP installer: %s", $@ || $! ));
} else {
    # Make sure that all requirements are met, else force execution of the
    # distribution installer bootstrap
    eval {
        prntInfo 'Check for i-MSCP requirements...';
        require iMSCP::Requirements;
        iMSCP::Requirements->new()->checkPrograms()->checkPhpModules()->checkPerlModules();
    };
    if ( $@ ) {
        prntInfo 'i-MSCP requirements are not met. Executing distribution installer bootstrap...';
        $forceDistBootstrap = 1;
        goto DIST_BOOTSTRAP;
    }
}

# checksum for amd64 (most used)
# TODO i386 checksum
unless ( system( 'echo 696bf0f5902ff1f58df1b0d7d7eacbb8 engine/PerlLib/iMSCP/H2ph.pm | md5sum -c > /dev/null 2>&1' ) == 0 ) {
    prntInfo 'Generating iMSCP::H2ph Perl package...';
    system( "cd engine/PerlLib; make clean all" ) == 0 or prntError( "Couldn't generate iMSCP::H2ph Perl package." );
}

# Execute the real installer
exec( '/usr/bin/perl', 'installer/installer.pl', @ARGV );

__END__
