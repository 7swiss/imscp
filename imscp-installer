#!/usr/bin/perl

=head1 NAME

 imscp-installer Install (or update and/or reconfigure) i-MSCP.

=head1 SYNOPSIS

 imscp-installer [OPTION]...

=cut

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2018 by Laurent Declercq <l.declercq@nuxwin.com>
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.

# Only Perl builtin and modules which are available in Perl base installation
# must be used in that script.

use strict;
use warnings;
use Cwd ();
use File::Basename ();
use lib "@{ [ Cwd::realpath( File::Basename::dirname( __FILE__ )) ] }/src/backend/lib";
use iMSCP::Boolean;
use iMSCP::Getopt;
use iMSCP::Installer::Bootstrapper;
use iMSCP::LockFile;
use POSIX 'locale_h';

BEGIN {
    local $@;
    # Get iMSCP::Debug or fake it
    eval { require iMSCP::Debug } or require iMSCP::Faker and $ENV{'IMSCP_FRESH_INSTALL'} = TRUE;
}

setlocale( LC_MESSAGES, 'C.UTF-8' );

@{ENV}{qw/ LANG IMSCP_INSTALLER IMSCP_DIST_INSTALLER / } = ( 'C.UTF-8', TRUE, TRUE );

$> == 0 or die( "You do not have permission to call this script.\n" );

exit unless iMSCP::LockFile->new( path => '/tmp/imscp-installer.lock', non_blocking => TRUE )->acquire();

{
    local @ARGV = @ARGV;
    iMSCP::Getopt->parse( sprintf( 'Usage: perl %s [OPTION]...', File::Basename::basename( $0 )) . qq{

Install (or update and/or reconfigure) i-MSCP.

OPTIONS:
 -c,   --clean-composer-cache         Cleanup composer cache.
 -d,   --debug                        Enable debug mode.
 -f,   --fix-permissions              Fix permissions recursively.
 -h,-? --help                         Show this help.
 -k,   --dist-package-file  <FILE>    Distribution package file.
 -l,   --layout             <FILE>    Distribution layout file.
 -n,   --non-interactive              Switch to non-interactive mode.
 -p,   --preseed <file>               i-MSCP preseeding file.
 -r,   --reconfigure        [ITEM]... Type 'help' for list of available items.
 -s,   --skip-dist-packages           Skip distribution packages.
 -v,   --verbose                      Enable verbose mode.
 -z,   --no-ansi                      Disable ANSI output.
},
        'clean-composer-cache|c' => \&iMSCP::Getopt::clearComposerCache,
        'debug|d'                => \&iMSCP::Getopt::debug,
        'dist-package-file|k=s'  => \&iMSCP::Getopt::distPackageFile,
        'fix-permissions|f'      => \&iMSCP::Getopt::fixPermissions,
        'help|?|h'               => \&iMSCP::Getopt::showUsage,
        'layout|l=s'             => \&iMSCP::Getopt::layout,
        'no-ansi|z'              => \&iMSCP::Getopt::noansi,
        'non-interactive|n'      => \&iMSCP::Getopt::noninteractive,
        'preseed|p=s'            => \&iMSCP::Getopt::preseed,
        'reconfigure|r:s{,}'     => \&iMSCP::Getopt::reconfigure,
        'skip-composer-update|a' => \&iMSCP::Getopt::skipComposerUpdate,
        'skip-dist-packages|s'   => \&iMSCP::Getopt::skipDistPackages,
        'verbose|v'              => \&iMSCP::Getopt::verbose
    );
}

iMSCP::Debug::newDebug( 'imscp-dist-installer.log' );

chdir $INC[0] . '/../../../' or die $!;

system( 'clear' ) unless iMSCP::Getopt->noninteractive;

# Satisfy i-MSCP installer prerequisites
iMSCP::Installer::Bootstrapper->new()->bootstrap();

# Execute the installer from distribution files location as the distribution
# files were not installed yet
exec 'perl', '-I', 'src/backend/blib/arch', '-I', 'src/backend/blib/lib', 'src/blib/script/imscp-installer', @ARGV or die $!;

=head1 AUTHOR

 Laurent Declercq <l.declercq@nuxwin.com>

=cut

1;
__END__
